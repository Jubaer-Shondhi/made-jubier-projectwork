pipeline CountryStatsPipeline {

  block CountryStatsExtractor oftype HttpExtractor {
    url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
  }

  
  block CountryStatsXLSXInterpreter oftype XLSXInterpreter { }

  block CountryStatsSheetPickerForBondIssuance oftype SheetPicker {
    sheetName: "Figure S5.1.2";
  }

  block CountryStatsSheetPickerForGDPPerCapita oftype SheetPicker {
    sheetName: "Figure S5.1.2";
  }


  block CountryStatsCellRangeSelectorForBondIssuance oftype CellRangeSelector {
    select: range P2:S45;
  }

  block CountryStatsCellRangeSelectorForGDPPerCapita oftype CellRangeSelector {
    select: range P2:S45;
  }


  block CountryStatsHeaderWriterCountryCodeForBondIssuance oftype CellWriter {
    at: cell P2;
    write: ["Country Code"];
  }

  block CountryStatsHeaderWriterForBondIssuance oftype CellWriter {
    at: cell S2;
    write: ["Bond Issuance Share"];
  }

  block CountryStatsHeaderWriterCountryCodeForGDPPerCapita oftype CellWriter {
    at: cell P2;
    write: ["Country Code"];
  }

  block CountryStatsHeaderWriterForGDPPerCapita oftype CellWriter {
    at: cell R2;
    write: ["GDP per Capita"];
  }

  block CountryStatsTableInterpreterForBondIssuance oftype TableInterpreter {
    header: true;
    columns: [
      "Country Code" oftype CountryCode,
      "Bond Issuance Share" oftype BondIssuanceShare
    ];
  }

  block CountryStatsTableInterpreterForGDPPerCapita oftype TableInterpreter {
    header: true;
    columns: [
      "Country Code" oftype CountryCode,
      "GDP per Capita" oftype GdpPerCapita
    ];
  }


  block BondIssuanceLoader oftype SQLiteLoader {
    table: "bondIssuance";
    file: "./country-stats.sqlite";
  }

  block GdpPerCapitaLoader oftype SQLiteLoader {
    table: "gdpPerCapita";
    file: "./country-stats.sqlite";
  }

  valuetype CountryCode oftype text {
    constraints: [ CCRegexConstraint ];
  }

  constraint CCRegexConstraint oftype RegexConstraint {
    regex: /^[A-Z]{3}$/;
  }

  valuetype BondIssuanceShare oftype decimal {
    constraints: [ BondIssuanceRange ];
  }

  constraint BondIssuanceRange oftype RangeConstraint {
    lowerBound: 0;
    upperBound: 1;
    lowerBoundInclusive: true;
    upperBoundInclusive: true;
  }

  valuetype GdpPerCapita oftype decimal {
    constraints: [ GdpPerCapitaRange ];
  }

  constraint GdpPerCapitaRange oftype RangeConstraint {
    lowerBound: 0;
    lowerBoundInclusive: true;
  }


  CountryStatsExtractor
    -> CountryStatsXLSXInterpreter
    -> CountryStatsSheetPickerForBondIssuance
    -> CountryStatsHeaderWriterCountryCodeForBondIssuance
    -> CountryStatsHeaderWriterForBondIssuance
    -> CountryStatsCellRangeSelectorForBondIssuance
    -> CountryStatsTableInterpreterForBondIssuance
    -> BondIssuanceLoader;

  CountryStatsXLSXInterpreter
    -> CountryStatsSheetPickerForGDPPerCapita
    -> CountryStatsHeaderWriterCountryCodeForGDPPerCapita
    -> CountryStatsHeaderWriterForGDPPerCapita
    -> CountryStatsCellRangeSelectorForGDPPerCapita
    -> CountryStatsTableInterpreterForGDPPerCapita
    -> GdpPerCapitaLoader;
}
